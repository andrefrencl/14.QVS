sub framework.weekday

    noconcatenate
    DiasSemana:
        load    * Inline [
        a, b, c
        'seg', '2º', 1
        'ter', '3º', 2
        'qua', '4º', 3
        'qui', '5º', 4
        'sex', '6º', 5
        'sáb', 'Sa', 6
        'dom', 'Do', 7 ];

        MAP_DiaSemanaAbrev:
        Mapping  load    a,b 
        Resident DiasSemana;

        MAP_DiaSemanaOrdem:
        Mapping  load    a,c 
        Resident DiasSemana;

        DROP	Table	DiasSemana;


end sub;

sub default.mastercalendar.PathQVD(p.fullPathFileQVD, p.DateFieldName, p.apelido, p.format,p.keyField)
    
        noconcatenate
        maxmin:
        load distinct   
            date(Min($(p.DateFieldName))-1,'$(p.format)')		as	mindate,
                    date(Max($(p.DateFieldName)),'$(p.format)')		as	maxdate
        From [$(p.fullPathFileQVD)](qvd)
                    ;
        call default.mastercalendar_Resident('maxmin', '$(p.DateFieldName)', '$(p.apelido)');
end sub;


/*
    let vDataInicio = num(date(date#('$(vAnoRetrasado)0101','YYYYMMDD')));
    Orders:
    LOAD * INLINE [
        OrderDate
        $(vDataInicio)
        $(vDataCarregado)
    ];
    call default.mastercalendar_Resident('Orders', 'OrderDate', 'db')
    call default.mastercalendar_Resident('dimensoes', '%orderdate', 'dim')
*/
sub default.mastercalendar_Resident(p.residentname, p.DateFieldName, p.apelido)

    LET vl.mxmin = Alt(NoOfRows('$(p.residentname)'),-1);
    
    if $(vl.mxmin) <> 1 then 
        load '$(p.fullPathFileQVD)' as filename
        from ERRORNOPROCESSOMASTERCALENDAR(qvd);
    endif 
    call framework.weekday;

    noconcatenate
    MasterCalendar$(p.apelido):
        load    
        TempDate 		                                                                                as [$(p.DateFieldName)],
        week(TempDate) 	                                                                                as [Week $(p.apelido)],
        Year(TempDate) 	                                                                                as [Year $(p.apelido)],
        Month(TempDate)	                                                                                as [Month $(p.apelido)],
        Num(Month(TempDate),'00') 	                                                                    as [Month Number $(p.apelido)],
        Day(TempDate) 		                                                                            as [Day $(p.apelido)],
        Year(TempDate) & '-' & Num(Month(TempDate),'00')                                                as [Year - Month Number $(p.apelido)],
        Right(Text(Year(TempDate)),2) & '-' & Num(Month(TempDate),'00')                                 as [Year - Month Name $(p.apelido)],
        'S' & ceil(month(TempDate) / 6)                                                                 AS [Semester $(p.apelido)],
        Year(TempDate) & '-S' & ceil(month(TempDate) / 6)                                               as [Year Semester $(p.apelido)],
        'T' & ceil(month(TempDate) / 3)                                                                 AS [Quarter $(p.apelido)],
        Week(weekstart(TempDate)) & '-' & WeekYear(TempDate)                                            as [Week of Year $(p.apelido)],
        WeekDay(TempDate) 	                                                                            as [Day of Week $(p.apelido)],
        ApplyMap('MAP_DiaSemanaAbrev',text(WeekDay([TempDate])),'nd')                                   as [Day Of Week Name $(p.apelido)],
        ApplyMap('MAP_DiaSemanaOrdem',text(WeekDay([TempDate])),7)                                      as [Day of Week Order $(p.apelido)],
        Num(Day(TempDate),'00') & '-' & ApplyMap('MAP_DiaSemanaAbrev',text(WeekDay([TempDate])),'nd')   as [Day of Week Number $(p.apelido)],

        If( Date(TempDate)>= (Date('$(conf.todayvar)')-7) 
            and Date(TempDate)<= (Date('$(conf.todayvar)')) , 1, 0)	                                    as [Is Last 7 Day $(p.apelido)],

        If( Date(TempDate)>= (Date('$(conf.todayvar)')-30) 
            and Date(TempDate)<= (Date('$(conf.todayvar)'))  , 1, 0)	                                as [Is Last 30 Day $(p.apelido)],

        If( DayNumberOfYear(Date(TempDate)) <= DayNumberOfYear(Date('$(conf.todayvar)')) , 1,0 ) 		as [Is In YTD $(p.apelido)],
        If( year(Date(TempDate)) = year(Date('$(conf.todayvar)'))
                ,'Is Current  Year','IsNOT Current Year')                                               as [Is Current Year $(p.apelido)],

        If( DayNumberOfQuarter(Date(TempDate)) <= DayNumberOfQuarter(Date('$(conf.todayvar)')), 1, 0)   as [Is In QTD $(p.apelido)],

        If( Month(Date(TempDate)) = Month(Date('$(conf.todayvar)')) 
            and year(Date(TempDate)) = year(Date('$(conf.todayvar)')),1,0)                              as [Is Current Of Year Month $(p.apelido)],
        If( Month(Date(TempDate)) = Month(Date('$(conf.todayvar)')),1,0)                                as [Is Current Month $(p.apelido)],
        If( WeekYear(Date(TempDate)) = WeekYear(Date('$(conf.todayvar)')) 
                and year(Date(TempDate)) = year(Date('$(conf.todayvar)')), 1, 0)                        as [Is Current week $(p.apelido)],
        If( Date(TempDate) = Date('$(conf.todayvar)'), 1, 0) 				                            as [Is Today $(p.apelido)],

        If( Month(Date(TempDate)) = month(addmonths(Date('$(conf.todayvar)'), -1))  
            and year(Date(TempDate)) = year(addmonths(Date('$(conf.todayvar)'), -1)) ,1,0)              as [Is Last Month $(p.apelido)],
        If( Year(Date(TempDate)) = year(addyears(Date('$(conf.todayvar)'), -1)) 
            or Year(Date(TempDate)) = year(Date('$(conf.todayvar)'))  ,1,0)                             as [Is Last Two Years $(p.apelido)],
        If( Year(Date(TempDate)) = year(addyears(Date('$(conf.todayvar)'), -1)),1,0)                    as [Is Last Year $(p.apelido)]
        ;
        load    
            date(mindate + IterNo()) AS TempDate
            ,maxdate // Used in InYearToDate() above, but not kept 
            WHILE mindate + IterNo() <= (maxdate);
            load distinct   
                        mindate-1 as mindate,
                        maxdate
            Resident $(p.residentname);

        drop table maxmin;
end sub;
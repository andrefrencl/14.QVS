//########################################################################################
// Script para montagem de tabelas "dimensoes" para o Qlik Sense
// Baseado nos extratores do Qlikview
// Autor: Andre Ramos       Data: 28/04/2022
//########################################################################################

sub start.dimensoes
	call fG.DataTransferValidarPodeProcessar('dimensoes', 'start.dimensoesCompleto',1);
end sub

sub start.dimensoesCompleto
	call dimensao.compradores;
	call dimensao.fornecedores.materiais;
	call dimensao.estrutura.comercial;
	call dimensao.cliente;
end sub

sub dimensao.compradores

	////////// carregando a carteira de compras ////////////////////
	////////// CRIADO por Andre Ramos
	
	//////////////////////////////////////////////////
	// SCRIPT para carga da CARTEIRA DE COMPRAS
	// CRIADO POR Andre Ramos
	//////////////////////////////////////////////////

	mapa_gercateg:
	Mapping
	LOAD Distinct
		[/BIC/CACDCPCAD]  as COD_GERENTE_CATEGORIA, 
		[/BIC/CADCCPDEN] as NOME_GERENTE_CATEGORIA
	FROM
	[$(paths.QVDExtract.ex_origem)BIC_PCACDCPCAD.QVD](qvd) 
	where len(Trim([/BIC/CACDCPCAD]))>0;

	mapa_ger_compras:
	Mapping
	LOAD Distinct
		[/BIC/CACDCPCAD]  as COD_GERENTE_COMPRAS, 
		[/BIC/CADCCPDEN] as NOME_GERENTE_COMPRAS
	FROM
	[$(paths.QVDExtract.ex_origem)BIC_PCACDCPCAD.QVD](qvd)
	where len(trim([/BIC/CACDCPCAD]))>0;

	mapa_comprador:
	Mapping
	LOAD Distinct
		[/BIC/CACDCPCAD]  as COD_COMPRADOR, 
		[/BIC/CADCCPDEN] as COMPRADOR
	FROM
	[$(paths.QVDExtract.ex_origem)BIC_PCACDCPCAD.QVD](qvd)
	where len(trim([/BIC/CACDCPCAD]))>0;

	mapa_forn:
	Mapping
	LOAD Distinct
		[/BIC/CA_VENDP] as COD_F, 
		TXTMD as NomeSubs
	FROM
	[$(paths.QVDExtract.ex_origem)BIC_TCA_VENDP.QVD](qvd)
	where len(trim([/BIC/CA_VENDP]))>0;

	// tabela que traz a relação dos compradores
	temp_comprador1:
	LOAD 
		[/BIC/CACDCPEST] as Subs, 
		ApplyMap('mapa_forn',[/BIC/CACDCPEST],null()) as NomeSubs,
		if([/BIC/CACDCPREG]='VLO','SC',[/BIC/CACDCPREG]) as ORIGEM_COMPRADOR, 

		[/BIC/CACDCPCMP] as COD_COMPRADOR, 
		applymap('mapa_comprador',[/BIC/CACDCPCMP],null()) as NOME_COMPRADOR,

		[/BIC/CACDCPGST] as COD_GERENTE_CATEGORIA,  
		ApplyMap('mapa_gercateg',[/BIC/CACDCPGST],null()) as NOME_GER_CATEGORIA,
		
		[/BIC/CACDCPGER] as COD_GERENTE_COMPRAS,
		ApplyMap('mapa_ger_compras',[/BIC/CACDCPGER],null()) as NOME_GER_COMPRAS,
		
		[/BIC/CACDCPCPP] as COD_COMPRADOR_PUBL, 
		applymap('mapa_comprador',[/BIC/CACDCPCPP],null()) as NOME_COMPRADOR_PUBL,
		
		[/BIC/CACDCPGTP] as COD_GERENTE_CATEGORIA_PUBL, 
		ApplyMap('mapa_gercateg',[/BIC/CACDCPGTP],null()) as NOME_GER_CATEGORIA_PUBL,
			
		[/BIC/CACDCPGRP] as COD_GERENTE_COMPRAS_PUBL,
		ApplyMap('mapa_ger_compras',[/BIC/CACDCPGRP],null()) as NOME_GER_COMPRAS_PUBL     
		
	FROM
	[$(paths.QVDExtract.ex_origem)BIC_PCACDCPEST.QVD](qvd);

	NoConcatenate
	temp_publico:
	load * Resident temp_comprador1
	where len(trim(COD_COMPRADOR_PUBL))>0;

	Concatenate(temp_comprador1)
	load
		Subs,
		NomeSubs,
		if(ORIGEM_COMPRADOR='VLO','SC',ORIGEM_COMPRADOR) as ORIGEM_COMPRADOR, 
		COD_COMPRADOR_PUBL as COD_COMPRADOR,
		NOME_COMPRADOR_PUBL as NOME_COMPRADOR,
		COD_GERENTE_CATEGORIA_PUBL as COD_GERENTE_CATEGORIA,
		NOME_GER_CATEGORIA_PUBL as NOME_GER_CATEGORIA,
		COD_GERENTE_COMPRAS_PUBL as COD_GERENTE_COMPRAS,
		NOME_GER_COMPRAS_PUBL as NOME_GER_COMPRAS,
		'X' as PUBLICO
	Resident temp_publico;
	drop table temp_publico;

	drop fields COD_COMPRADOR_PUBL,NOME_COMPRADOR_PUBL,COD_GERENTE_CATEGORIA_PUBL
				,NOME_GER_CATEGORIA_PUBL,COD_GERENTE_COMPRAS_PUBL,NOME_GER_COMPRAS_PUBL
		from temp_comprador1;

	NoConcatenate
	DIM_COMPRADOR:
	Load  distinct
		Subs&'-'&if(ORIGEM_COMPRADOR='SC','SANTACRUZ','PANPHARMA') as Key_COMPRADOR,
		upper(NOME_COMPRADOR) as COMPRADOR,
		upper(NOME_GER_CATEGORIA) as GERENTE_CATEGORIA,
		upper(NOME_GER_COMPRAS) as GERENTE_COMPRAS
	Resident temp_comprador1
	where wildmatch(ORIGEM_COMPRADOR,'SC','PAN');
	drop table temp_comprador1;

end sub

sub dimensao.fornecedores.materiais

	Trace *************************************************************************;
	Trace DIM_MATERIAIS ;
	Trace *************************************************************************;
	
	//// TUDO numa TABELA Sï¿½
	
	NoConcatenate
	DIM_MATERIAIS:
	LOAD 
		COD_MATERIAL&'-'&DES_CD_M AS [Key Cod Mat CD],
		num(right(COD_MATERIAL,6),0) as COD_MATERIAL_SAP,// COD_SAP, 	
		//DES_CD_M as DES_CD,
		//COD_FORNECEDOR_PFAT,// as COD_SUBSORTIMENTO,	
	 	COD_BAND_GMR,
	 	DES_BAND_GMR,
	 	DES_BAND_FORNECEDOR,// as DES_FORNECEDOR,
	 	Pacote, // as CATEGORIA_PRODUTO,
	    COD_ORIGEM&' - '&DES_ORIGEM AS DES_ORIGEM
	 FROM
	[$(paths.QVDExtract.ex_dimensoes)BW_MATERIAL_FORNECEDOR_CD.QVD](qvd)
	where len(trim(DES_CD_M))>0 or len(trim(COD_MATERIAL))>0;
	
	
	left join (DIM_MATERIAIS)
	LOAD 
		num(right(COD_MATERIAL_SAPa,6),0) as COD_MATERIAL_SAP,// COD_SAP, 	
	     ULT_COD_SCRUZ, // as COD_MATERIAL_PFAT, 
	     COD_EAN_MATERIAL, 
	     NME_MATERIAL
	FROM
	[$(paths.QVDExtract.ex_dimensoes)BW_MATERIAL.QVD]
	(qvd)
	where len(trim(COD_MATERIAL_SAPa))>0;
	
	Mapping	
	MAP_MARCA:
	LOAD ULT_COD_SCRUZ, 
	     MARCA_DESC as MARCA
	FROM [$(paths.QVDExtract.ex_arquivos0)Marca.xlsx]
	(ooxml, embedded labels, table is Planilha1);
	
	DIM_PRODUTOS_CLOSEUP:
	LOAD Distinct
	     ULT_PFAT as ULT_COD_SCRUZ,// as COD_MATERIAL_PFAT, 
	     if(len(trim(MARCA_DESC))=0, ApplyMap('MAP_MARCA',ULT_PFAT,null()),MARCA_DESC) as MARCA_DESC,
	     MOLECULA_DESC, 
	     CLASSE_I, 
	     CLASSE_II, 
	     CLASSE_III, 
	     CLASSE_IV, 
	     EAN_CUP
	FROM
	[$(paths.QVDExtract.ex_entradas_closeup)FLASH_CLOSEUP_Produtos.QVD]
	(qvd)
	where len(trim(ULT_PFAT))>0 ;
	

end sub

sub dimensao.estrutura.comercial

	Trace *************************************************************************;
	Trace DIM_ESTRUTURA_COMERCIAL ;
	Trace *************************************************************************;
	
	DIM_ESTRUTURA_COMERCIAL:
	LOAD Distinct
	      COD_CLIENTE_SAP&'-'&COD_SETOR_ATIVIDADE&'-'&COD_ORGANIZACAO_VENDAS &'-'&COD_CANAL_DISTRIBUICAO as [Key Cliente], 
	      COD_SETOR, 
	      COD_SUPERVISOR, 
	      COD_REGIAO, 
	      NME_GRV,
	      NME_GV,
	      NME_REGIAO, 
	      NME_SUPERVISOR, 
	      NME_SETOR
	FROM [$(paths.QVDExtract.ex_dimensoes)BW_ESTRUTURA_COMERCIAL.QVD] (qvd)
	where len(trim(NME_GRV))>0;

end sub

sub dimensao.cliente

	Trace *************************************************************************;
	Trace DIM_BANDEIRA e DIM_CLIENTES ;
	Trace *************************************************************************;
	
	//-------------------------------------------------------------
	temp:
	LOAD CAMINHO, 
	     ARQUIVO,
	     PLANILHA
	FROM
	[$(paths.QVDExtract.ex_arquivos0)Endereco_Arquivos.xlsx]
	(ooxml, embedded labels, table is Plan1)
	where CHAVE = 'BANDAGRUPADO';
	let vPasta = peek('CAMINHO',0,'temp');
	let vArquivo = peek('ARQUIVO',0,'temp');
	let vPlanilha = peek('PLANILHA',0,'temp');
	drop table temp;
	
	mapa_agru:
	Mapping
	LOAD Distinct
		 BANDEIRA, 
	     BANDEIRA_AGRUPADA
	FROM
	$(vPasta)$(vArquivo)
	(ooxml, embedded labels, table is $(vPlanilha))
	where len(trim(BANDEIRA_AGRUPADA))>0;
	
	mapa_assoc:
	Mapping
	LOAD Distinct BANDEIRA, 
	     ASSOCIAÇÃO as ASSOCIACAO
	FROM
	$(vPasta)$(vArquivo)
	(ooxml, embedded labels, table is $(vPlanilha))
	where len(trim(ASSOCIAÇÃO))>0;
	
	DIM_BANDEIRA_CLIENTE:
	LOAD Distinct
	      COD_CLIENTE_SAP&'-'&COD_SETOR_ATIVIDADE&'-'&COD_ORGANIZACAO_VENDAS &'-'&COD_CANAL_DISTRIBUICAO as [Key Cliente], 
	//      num(right(COD_CLIENTE_SAP,6)) as COD_CLIENTE_SAP,
	      COD_BANDEIRA_CLIENTE, 
	      NME_BANDEIRA_CLIENTE, 
	      NME_SEGMT_CLIENTE,
	      ApplyMap('mapa_agru',NME_BANDEIRA_CLIENTE,null()) as BANDEIRA_AGRUPADA,
	      ApplyMap('mapa_assoc',NME_BANDEIRA_CLIENTE,null()) as ASSOCIACAO
	FROM [$(paths.QVDExtract.ex_dimensoes)BW_ESTRUTURA_COMERCIAL.QVD] (qvd)
	where len(trim(COD_BANDEIRA_CLIENTE))>0;
	      
	
	DIM_CLIENTE:      
	LOAD Distinct
	     num(right(COD_CLIENTE_SAP,6)) as COD_CLIENTE_SAP,
	     NUM_CNPJ, 
	     NUM_CPF, 
	     NME_RAZAO_SOCIAL, 
	     NME_CLIENTE_REDUZIDO, 
	     NME_ENDERECO_CLIENTE, 
	     NME_CIDADE_CLIENTE, 
	     NME_BAIRRO_CLIENTE, 
	 	 COD_UF_CLIENTE, 
	     NME_UF_CLIENTE,
	     DES_GRUPO_CLIENTE 
	//     COD_CLIENTE_PFAT
	FROM [$(paths.QVDExtract.ex_dimensoes)BW_Customer.QVD] (qvd)
	where len(trim(COD_CLIENTE_SAP))>0;
	
end sub